'' Name: INDEXER: Align Mandrel
' Description: Check alignment of Mandrel on Indexer
'   
' Platform: Handibot2
' Author: Gordon Bergrfors, Ted Hall
' History:
'   2017/11/11 - Initial version (th)
'   2018/04/14 - (th) Will save system variables for $z_topMandrel_toCenter and $x_mandrel_offset 

SU, 0            ' Set to Inches; Units will auto-revert to current starting units at end of file

'--- Settings ---
&ShaftDia = 0.2500  ' of drill rod
&HubDiameter = 1.4095
&HubRadius = (&HubDiameter/2) + 0.1
$ContactInput = 1
&SearchDistance = 4.0 
&SearchSpeedSlow = 0.2
&SearchSpeedFast = 0.5
&backoff = 0.5

MS, 1, 1	' Speeds will auto-revert back to current values at end of file
JS, 3, 3

'notes: depth not quite right on initial plunge, side of hub and mandrel plunge ... not quite wide enough on hub; clips a problem;
'           ... need to incorporate variables

' -------- Main Program Section --------
   PAUSE "Attach ZZero Alligator Clip to Cutter Shank and Jumper Clip to Indexer (see App Instructions) . -Resume- when Ready"
     ' To Test Clips, touch to ground and Input #1 should come on.    
   GOSUB check_input

   'Start at top of mandrel hub
       &xpos = %(1)
       &ypos = %(2)
       &zpos = %(3)
       &bpos = %(5)
     GOSUB touch_in_z
     GOSUB find_center_wB_rotation
       &Hub = (&rightLoc - &leftLoc)
       &HubDiameter = &Hub - &ShaftDia
   
   PAUSE "Hub Diameter = " + &HubDiameter
      &Radius = &HubDiameter/2
      &HubRadius = &Radius + 0.1
      MZ, %(3) + 0.25

   PAUSE "Setting Z Offset from Mandrel = " + &Radius
      &temp_z_topMandrel_toCenter = &Radius

   'Move to front on mandrel shaft
      MY, %(2) + 1.25
      GOSUB touch_in_z
      GOSUB find_center_front
   'Move to back on mandrel shaft
      MY, %(2) + 4.5
      GOSUB touch_in_z
      GOSUB find_center_back
    MZ, &zpos
    M2, , &ypos        ' Back to Starting Position
    PAUSE "If Alignment Acceptable Click -RESUME- to SAVE Else QUIT"
	  $z_topMandrel_toCenter = &temp_z_topMandrel_toCenter
	  $x_mandrel_offset = &temp_x_mandrel_offset
END

' --- Subroutines ---
touch_in_z:    
   ' Touch Off of top 
   PZ, %(3) - &SearchDistance, &SearchSpeedSlow, $ContactInput
   &topZ = %(3)
   ' Go to plate location
   RETURN

find_center_wB_rotation:   
   MZ, &topZ + 0.125
   MB, &bpos + 90
   MX, %(1) - 1
   MZ, &topZ - &HubRadius
     PX, %(1) + &SearchDistance, &SearchSpeedSlow, $ContactInput
     &leftLoc = %(1)
     MX, %(1) - &backoff
   MZ, &topZ + 0.25
   MX, &xpos + 1
   MZ, &topZ - &HubRadius
     PX, %(1) - &SearchDistance, &SearchSpeedSlow, $ContactInput
     &rightLoc = %(1) 
     MX, %(1) + &backoff
   MZ, &topZ + 0.125
   MX, ((&leftLoc + &rightLoc) / 2)
   MB, &bpos
     PZ, %(3) - &SearchDistance, &SearchSpeedSlow, $ContactInput
     &now_X = %(1)
     &now_Z = %(3)
     MZ, &now_Z + 0.10   ' Need a little move so PAUSE works
     PAUSE "Centered at a Height, X = " + &now_X + ",   Z = " + &now_Z
     &mandrel_from_Zzero = &now_Z
   MZ, %(3) + 0.25
   RETURN
  
find_center_front:       ' without B rotation   
   MZ, &topZ + 0.125
   MX, %(1) - 1
   MZ, &topZ - 0.25
     PX, %(1) + &SearchDistance, &SearchSpeedSlow, $ContactInput
     &leftLoc = %(1)
     MX, %(1) - &backoff
   MZ, &topZ + 0.25
   MX, &xpos + 1
   MZ, &topZ - 0.25
     PX, %(1) - &SearchDistance, &SearchSpeedSlow, $ContactInput
     &rightLoc = %(1) 
     MX, %(1) + &backoff
   MZ, &topZ + 0.125
   MX, ((&leftLoc + &rightLoc) / 2)
     PZ, %(3) - &SearchDistance, &SearchSpeedSlow, $ContactInput
     &front_X = %(1)
     &front_Z = %(3)
     &front_deviation = (&mandrel_from_Zzero - &Radius) - (%(3) - 0.125)
     MZ, &front_Z + 0.10   ' Need a little move so PAUSE works
     PAUSE "Centered at a Height, X = " + &front_X + ",   Z = " + &front_Z
   MZ, &topZ + 0.25
   PAUSE "Z deviation from hub = " + &front_deviation
   RETURN

find_center_back:       ' without B rotation   
   MZ, &topZ + 0.125
   MX, %(1) - 1
   MZ, &topZ - 0.25
     PX, %(1) + &SearchDistance, &SearchSpeedSlow, $ContactInput
     &leftLoc = %(1)
     MX, %(1) - &backoff
   MZ, &topZ + 0.25
   MX, &xpos + 1
   MZ, &topZ - 0.25
     PX, %(1) - &SearchDistance, &SearchSpeedSlow, $ContactInput
     &rightLoc = %(1) 
     MX, %(1) + &backoff
   MZ, &topZ + 0.125
   MX, ((&leftLoc + &rightLoc) / 2)
     PZ, %(3) - &SearchDistance, &SearchSpeedSlow, $ContactInput
     &back_X = %(1)
     &back_Z = %(3)
     MZ, &back_Z + 0.10   ' Need a little move so PAUSE works
     PAUSE "Centered at a Height, X = " + &back_X + ",   Z = " + &back_Z
   MZ, &topZ + 0.25
   PAUSE "X deviation of shaft = " + (&front_X - &back_X) + ",   Z deviation of shaft = " + (&front_Z - &back_Z)  
     &temp_x_mandrel_offset = (&front_X + &back_X) / 2
   RETURN

check_input:
    IF %(51) = 1 THEN GOTO check_input_fail
	RETURN

check_input_fail:
  'Input switch is already tripped. Cannot zero the tool. Quit, Move Off Switch, and Retry.
	PAUSE
' Following format not working yet ...
'    END "Input switch is already tripped. Cannot zero the tool. Move Off Switch and Retry."

END

